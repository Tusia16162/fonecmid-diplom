
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// ВКМ Начало доработки
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	ВКМ_СформироватьОсновныеНачисления();
    Движения.ВКМ_ОсновныеНачисления.Записать();
	
	Движения.ВКМ_Удержания.Записывать = Истина;
	ВКМ_СформироватьУдержания();	 
	Движения.ВКМ_Удержания.Записать();

	ВКМ_РасчетОклада(Движения.ВКМ_ОсновныеНачисления);
	ВКМ_РасчетОтпуска(Движения.ВКМ_ОсновныеНачисления);
	ВКМ_РасчетНДФЛ(Движения.ВКМ_Удержания);
	ВКМ_СформироватьДвиженияВзаиморасчетов();
	// ВКМ Конец докработки
	
КонецПроцедуры

// ВКМ Начало доработки
Процедура ВКМ_СформироватьОсновныеНачисления()
	
	ГрафикРаботы = Справочники.ВКМ_ГрафикРаботы.НайтиПоНаименованию("Пятидневка");
	
	Для Каждого ТекСтрока Из ОсновныеНачисления Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрока.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрока.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(ТекСтрока.ДатаЗавершения); 
		Движение.БазовыйПериодНачало = НачалоМесяца(ПериодРегистрации);
		Движение.БазовыйПериодКонец = КонецМесяца(ПериодРегистрации);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.ВКМ_Сотрудник = ТекСтрока.Сотрудник;
		Движение.ВКМ_Результат = 0;
		Движение.ВКМ_ГрафикРаботы = ГрафикРаботы; 

	КонецЦикла; 
	
КонецПроцедуры 

Процедура ВКМ_СформироватьУдержания()	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеНачисления.Сотрудник КАК Сотрудник
	|ИЗ
	|	Документ.ВКМ_НачисленияЗарплаты.ОсновныеНачисления КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОсновныеНачисления.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДвижениеНДФЛ = Движения.ВКМ_Удержания.Добавить();
		ДвижениеНДФЛ.Сторно = Ложь;
		ДвижениеНДФЛ.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.ВКМ_НДФЛ;
		ДвижениеНДФЛ.ПериодРегистрации = ПериодРегистрации;
		ДвижениеНДФЛ.БазовыйПериодНачало = НачалоМесяца(ПериодРегистрации);
		ДвижениеНДФЛ.БазовыйПериодКонец = КонецМесяца(ПериодРегистрации);
		ДвижениеНДФЛ.ВКМ_Сотрудник = Выборка.Сотрудник;  
		ДвижениеНДФЛ.ВКМ_Результат = 0;

	КонецЦикла;
	
КонецПроцедуры

Процедура ВКМ_РасчетОклада(ДвиженияОсновные)
	
	ПрошлыйМесяц = НачалоМесяца(Дата) - 1;
	НачалоПериода = НачалоМесяца(ПрошлыйМесяц);
	КонецПериода = КонецМесяца(ПрошлыйМесяц);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета КАК ВидРасчета,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник.Специалист КАК СотрудникСпециалист,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеПериодДействия КАК ВКМ_ЗначениеПериодДействия,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеФактическийПериодДействия КАК ВКМ_ЗначениеФактическийПериодДействия
	|ПОМЕСТИТЬ ВТ_ОсновныеНачисления
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|			Регистратор = &Регистратор
	|				И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Оклад КАК ВКМ_Оклад,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_ПроцентОтРабот КАК ВКМ_ПроцентОтРабот
	|ПОМЕСТИТЬ ВТ_Условия
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(
	|			&КонецПериода,
	|			ВКМ_Сотрудник В
	|				(ВЫБРАТЬ
	|					ВТ_ОсновныеНачисления.ВКМ_Сотрудник КАК ВКМ_Сотрудник
	|				ИЗ
	|					ВТ_ОсновныеНачисления КАК ВТ_ОсновныеНачисления)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_СуммаКОплатеПриход КАК ВКМ_СуммаКОплатеПриход
	|ПОМЕСТИТЬ ВТ_Поступления
	|ИЗ
	|	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Месяц,
	|			ВКМ_Сотрудник В
	|				(ВЫБРАТЬ
	|					ВТ_ОсновныеНачисления.ВКМ_Сотрудник КАК ВКМ_Сотрудник
	|				ИЗ
	|					ВТ_ОсновныеНачисления КАК ВТ_ОсновныеНачисления)) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ВТ_ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
	|	ВТ_ОсновныеНачисления.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|	ВТ_ОсновныеНачисления.СотрудникСпециалист КАК СотрудникСпециалист,
	|	ЕСТЬNULL(ВТ_Условия.ВКМ_Оклад,0) КАК ВКМ_Оклад,
	|	ЕСТЬNULL(ВТ_Поступления.ВКМ_СуммаКОплатеПриход,0) КАК ВКМ_СуммаКОплатеПриход,
	|	ЕСТЬNULL(ВТ_ОсновныеНачисления.ВКМ_ЗначениеПериодДействия,0) КАК План,
	|	ЕСТЬNULL(ВТ_ОсновныеНачисления.ВКМ_ЗначениеФактическийПериодДействия,0) КАК Факт
	|ИЗ
	|	ВТ_ОсновныеНачисления КАК ВТ_ОсновныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Условия КАК ВТ_Условия
	|		ПО ВТ_ОсновныеНачисления.ВКМ_Сотрудник = ВТ_Условия.ВКМ_Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Поступления КАК ВТ_Поступления
	|		ПО ВТ_ОсновныеНачисления.ВКМ_Сотрудник = ВТ_Поступления.ВКМ_Сотрудник";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Оклад);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для Каждого Запись Из ДвиженияОсновные Цикл
		СтруктураПоиска = Новый Структура("НомерСтроки", Запись.НомерСтроки);
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Если Запись.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Оклад Тогда
				Если Выборка.СотрудникСпециалист = Истина Тогда
					ИтоговыйОклад = Выборка.ВКМ_Оклад + Выборка.ВКМ_СуммаКОплатеПриход; 
					Запись.ВКМ_Результат = ИтоговыйОклад / Выборка.План * Выборка.Факт;  
				Иначе
					Запись.ВКМ_Результат = Выборка.ВКМ_Оклад / Выборка.План * Выборка.Факт;
				КонецЕсли;
			Иначе
				Запись.ВКМ_Результат = 0;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ДвиженияОсновные.Записать();
	
КонецПроцедуры

Процедура ВКМ_РасчетОтпуска(ДвиженияОсновные)
	
	НачалоПериода = НачалоМесяца(ДобавитьМесяц(ПериодРегистрации, -12));
	КонецПериода = КонецМесяца(ПериодРегистрации);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|	СУММА(ВКМ_ГрафикиРаботы.ВКМ_Значение) КАК ЧасовОтпуска,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_ПериодОтпуска
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|			Регистратор = &Регистратор
	|				И ВидРасчета = &ВидРасчетаОтпуск) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_ГрафикиРаботы КАК ВКМ_ГрафикиРаботы
	|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало <= ВКМ_ГрафикиРаботы.ВКМ_Дата
	|			И ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец >= ВКМ_ГрафикиРаботы.ВКМ_Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|	СУММА(ВКМ_ГрафикиРаботы.ВКМ_Значение) КАК ЧасовОклада
	|ПОМЕСТИТЬ ВТ_ПериодНачислений
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|			ВКМ_Сотрудник В
	|					(ВЫБРАТЬ
	|						ВТ_ПериодОтпуска.ВКМ_Сотрудник КАК ВКМ_Сотрудник
	|					ИЗ
	|						ВТ_ПериодОтпуска КАК ВТ_ПериодОтпуска)
	|				И ВидРасчета = &ВидРасчетаОклад
	|				И ПериодДействияНачало >= &НачалоПериода
	|				И ПериодДействияКонец <= &КонецПериода) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_ГрафикиРаботы КАК ВКМ_ГрафикиРаботы
	|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало <= ВКМ_ГрафикиРаботы.ВКМ_Дата
	|			И ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец >= ВКМ_ГрафикиРаботы.ВКМ_Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|	СУММА(ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Результат) КАК ИтоговаяСумма
	|ПОМЕСТИТЬ ВТ_ИтоговаяСумма
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|			ВКМ_Сотрудник В
	|					(ВЫБРАТЬ
	|						ВТ_ПериодОтпуска.ВКМ_Сотрудник КАК ВКМ_Сотрудник
	|					ИЗ
	|						ВТ_ПериодОтпуска КАК ВТ_ПериодОтпуска)
	|				И ВидРасчета = &ВидРасчетаОклад
	|				И ПериодДействияНачало >= &НачалоПериода
	|				И ПериодДействияКонец <= &КонецПериода) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПериодОтпуска.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|	ВТ_ПериодОтпуска.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПериодОтпуска.ЧасовОтпуска КАК ЧасовОтпуска,
	|	ВТ_ПериодНачислений.ЧасовОклада КАК ЧасовОклада,
	|	ВТ_ИтоговаяСумма.ИтоговаяСумма КАК ИтоговаяСумма
	|ИЗ
	|	ВТ_ПериодОтпуска КАК ВТ_ПериодОтпуска
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодНачислений КАК ВТ_ПериодНачислений
	|		ПО ВТ_ПериодОтпуска.ВКМ_Сотрудник = ВТ_ПериодНачислений.ВКМ_Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтоговаяСумма КАК ВТ_ИтоговаяСумма
	|		ПО ВТ_ПериодОтпуска.ВКМ_Сотрудник = ВТ_ИтоговаяСумма.ВКМ_Сотрудник";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчетаОтпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Отпуск);
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Оклад);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для Каждого Запись Из  ДвиженияОсновные Цикл
		СтруктураПоиска = Новый Структура("НомерСтроки", Запись.НомерСтроки);
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Если Запись.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Отпуск Тогда
				Запись.ВКМ_Результат = Выборка.ИтоговаяСумма / Выборка.ЧасовОклада * Выборка.ЧасовОтпуска;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ДвиженияОсновные.Записать();

КонецПроцедуры 

Процедура ВКМ_РасчетНДФЛ(ДвиженияУдеожания)

	Измерения = Новый Массив;
	Измерения.Добавить("ВКМ_Сотрудник");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдержанияОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	УдержанияОсновныеНачисления.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|	УдержанияОсновныеНачисления.ВКМ_РезультатБаза КАК ВКМ_РезультатБаза
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК УдержанияОсновныеНачисления";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать(); 
	
	Для Каждого Запись Из ДвиженияУдеожания Цикл
		СтруктураПоиска = Новый Структура("НомерСтроки", Запись.НомерСтроки);
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			СуммаНДФЛ = Выборка.ВКМ_РезультатБаза / 100 * 13;
			Запись.ВКМ_Результат = СуммаНДФЛ;
		КонецЕсли;
	КонецЦикла;
	
	ДвиженияУдеожания.Записать();
	
КонецПроцедуры

Процедура ВКМ_СформироватьДвиженияВзаиморасчетов() 
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.ВКМ_Сотрудник КАК Сотрудник,
	|	ВКМ_ОсновныеНачисления.ВКМ_Результат КАК Сумма
	|ПОМЕСТИТЬ ДанныеРегистра
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_Удержания.ВКМ_Сотрудник,
	|	ВКМ_Удержания.ВКМ_Результат
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Сотрудник КАК Сотрудник,
	|	СУММА(ДанныеРегистра.Сумма) КАК Сумма
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Сотрудник";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	  Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
	  Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	  Движение.Период = ПериодРегистрации;
	  Движение.Сотрудник = Выборка.Сотрудник;
	  Движение.Сумма = Выборка.Сумма;
      Движение.Регистратор = Ссылка;
	КонецЦикла;

КонецПроцедуры

// ВКМ Конец докработки